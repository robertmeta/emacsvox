;;; emacsvox-ecb.el --- speech-enable ECB -*- lexical-binding: t; -*-
;;
;; $Author: tv.raman.tv $
;; Description:  Emacsvox module for speech-enabling Emacs
;; Class Browser
;; Keywords: Emacsvox, ecb
;;;   LCD Archive entry:

;; LCD Archive Entry:
;; emacsvox| T. V. Raman |tv.raman.tv@gmail.com
;; A speech interface to Emacs |
;; 
;;  $Revision: 4074 $ |
;; Location https://github.com/tvraman/emacsvox
;; 

;;;   Copyright:

;; Copyright (C) 1995 -- 2024, T. V. Raman
;; All Rights Reserved.
;; 
;; This file is not part of GNU Emacs, but the same permissions apply.
;; 
;; GNU Emacs is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2, or (at your option)
;; any later version.
;; 
;; GNU Emacs is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;; 
;; You should have received a copy of the GNU General Public License
;; along with GNU Emacs; see the file COPYING.  If not, write to
;; the Free Software Foundation, 51 Franklin Street, Fifth Floor,
;; Boston, MA 02110-1301, USA.

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Commentary:
;; The ECB is an Emacs Class Browser.
;; This module speech-enables ECB
;;; Code:


;;  required modules

(cl-declaim  (optimize  (safety 0) (speed 3)))
(require 'emacsvox-preamble)

;;;  Forward Declarations:

(declare-function tree-node->expandable "tree-buffer" (cl-x))
(declare-function tree-node->expanded "tree-buffer" (cl-x))
(declare-function ecb-goto-window-methods "ecb-method-browser" nil)
(declare-function ecb-goto-window-directories "ecb-file-browser" nil)
(declare-function ecb-goto-window-history "ecb-file-browser" nil)
(declare-function ecb-goto-window-sources "ecb-file-browser" nil)

;;;   advice interactive commands

(defun ems--ecb-activate-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'open-object) (emacsvox-speak-mode-line)))

(advice-add 'ecb-activate :after #'ems--ecb-activate-after)

(defun ems--ecb-cancel-dialog-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'close-object) (emacsvox-speak-mode-line)))

(advice-add 'ecb-cancel-dialog :after #'ems--ecb-cancel-dialog-after)

(defun ems--ecb-show-help-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'help) (emacsvox-speak-mode-line)))

(advice-add 'ecb-show-help :after #'ems--ecb-show-help-after)

(defun ems--ecb-nav-goto-next-after (&rest _)
  "speak."
  (when (ems-interactive-p)
       (emacsvox-speak-line)
       (emacsvox-icon 'select-object)))

(cl-loop
 for f in
 '(ecb-nav-goto-next ecb-nav-goto-previous ecb-goto-window-compilation ecb-goto-window-directories ecb-goto-window-sources ecb-goto-window-methods ecb-goto-window-history ecb-goto-window-edit1 ecb-goto-window-edit2)
 do
 (advice-add f :after #'ems--ecb-nav-goto-next-after))

(defun ems--ecb-select-ecb-frame-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-speak-mode-line) (emacsvox-icon 'select-object)))

(advice-add 'ecb-select-ecb-frame :after
            #'ems--ecb-select-ecb-frame-after)

;;;   inform tree browser about emacsvox

;; define emacsvox versions of these special tree search
;; commands
;; need these to get ECB working outside X

(defvar tree-buffer-incr-searchpattern)
(defvar tree-buffer-key-map)

(defun emacsvox-ecb-tree-backspace ()
  "Back up during incremental search in tree buffers."
  (interactive)
  
  ;; reduce by one from the end
  (setq tree-buffer-incr-searchpattern
        (substring tree-buffer-incr-searchpattern
                   0
                   (max 0 (1- (length
                               tree-buffer-incr-searchpattern)))))
  (dtk-speak  tree-buffer-incr-searchpattern)
  (emacsvox-icon 'delete-object))

(defun emacsvox-ecb-tree-clear ()
  "Clear search pattern during incremental search in tree buffers."
  (interactive)
  
  (setq tree-buffer-incr-searchpattern "")
  (dtk-speak "Cleared search pattern."))

(defun ems--tree-buffer-create-after (_0 _1 _2 _3 _4 _5 _6 _7 _8 _9 &optional incr-search &rest _)
  "Fixes up keybindings so incremental tree search is\navailable."
  (when incr-search
    (substitute-key-definition 'emacsvox-self-insert-command
                               'tree-buffer-incremental-node-search
                               tree-buffer-key-map global-map)
    (define-key tree-buffer-key-map " " 'emacsvox-ecb-tree-backspace)
    (define-key tree-buffer-key-map '[delete]
                'emacsvox-ecb-tree-backspace)
    (define-key tree-buffer-key-map '[home] 'emacsvox-ecb-tree-clear)))

(advice-add 'tree-buffer-create :after #'ems--tree-buffer-create-after)

(defun ems--tree-buffer-incremental-node-search-around
    (orig-fun &rest args)
  "Track search and provide appropriate auditory feedback."
  (if (not (ems-interactive-p))
      (apply orig-fun args)
    (let* ((start (point)) (beg nil) (end nil)
           (res (apply orig-fun args)))
      (cond
       ((not (= start (point)))
        (let ((emacsvox-speak-messages nil) (case-fold-search t))
          (save-excursion
            (beginning-of-line) (setq beg (point)) (backward-char 1)
            (search-forward tree-buffer-incr-searchpattern)
            (setq end (point))
            (with-silent-modifications
              (ems-set-personality-temporarily beg end voice-bolden
                                               (emacsvox-speak-line)))
            (emacsvox-icon 'search-hit))))
       (t (emacsvox-icon 'search-miss)))
      res)))

(advice-add 'tree-buffer-incremental-node-search :around
            #'ems--tree-buffer-incremental-node-search-around)

(defun ems--tree-buffer-select-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'select-object) (emacsvox-speak-line)))

(advice-add 'tree-buffer-select :after #'ems--tree-buffer-select-after)

(defun tree-node-is-expanded (node)
  "Check if node is expanded."
  (or (not (tree-node->expandable node))
      (tree-node->expanded node)))

(defun ems--tree-node-toggle-expanded-after (node &rest _)
  "speak."
  (when (ems-interactive-p)
    (cond
     ((tree-node-is-expanded node) (emacsvox-icon 'open-object))
     (t (emacsvox-icon 'close-object)))))

(advice-add 'tree-node-toggle-expanded :after
            #'ems--tree-node-toggle-expanded-after)

(defun ems--tree-buffer-update-after (&rest _)
  "Provide context speech feedback."
  (when (ems-interactive-p) (emacsvox-speak-line)))

(advice-add 'tree-buffer-update :after #'ems--tree-buffer-update-after)

(defun ems--tree-buffer-nolog-message-around (orig-fun &rest args)
  "Speak the message."
  (let ((res (apply orig-fun args)))
    (dtk-speak res)
    res))

(advice-add 'tree-buffer-nolog-message :around
            #'ems--tree-buffer-nolog-message-around)

(defun ems--tree-buffer-arrow-pressed-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'button) (emacsvox-speak-line)))

(advice-add 'tree-buffer-arrow-pressed :after
            #'ems--tree-buffer-arrow-pressed-after)

(defun ems--tree-buffer-tab-pressed-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'button) (emacsvox-speak-line)))

(advice-add 'tree-buffer-tab-pressed :after
            #'ems--tree-buffer-tab-pressed-after)

(defun ems--tree-buffer-return-pressed-after (&rest _)
  "speak."
  (when (ems-interactive-p)
    (emacsvox-icon 'button) (emacsvox-speak-line)))

(advice-add 'tree-buffer-return-pressed :after
            #'ems--tree-buffer-return-pressed-after)

(defun ems--tree-buffer-show-menu-keyboard-around
    (orig-fun &rest args)
  "When on the console, always use TMM."
  (cond
   ((and (ems-interactive-p) (not (display-graphic-p)))
    (tree-buffer-show-menu-keyboard 'use-tmm))
   (t (apply orig-fun args))))

(advice-add 'tree-buffer-show-menu-keyboard :around
            #'ems--tree-buffer-show-menu-keyboard-around)

;;;  commands to speak ECB windows without  moving

(defun emacsvox-ecb-speak-window-methods ()
  "Speak contents of methods window."
  (interactive)
  (save-excursion
    (save-window-excursion
      (ecb-goto-window-methods)
      (emacsvox-speak-buffer))))

(defun emacsvox-ecb-speak-window-directories ()
  "Speak contents of directories window."
  (interactive)
  (save-excursion
    (save-window-excursion
      (ecb-goto-window-directories)
      (emacsvox-speak-buffer))))

(defun emacsvox-ecb-speak-window-history ()
  "Speak contents of history window."
  (interactive)
  (save-excursion
    (save-window-excursion
      (ecb-goto-window-history)
      (emacsvox-speak-buffer))))

(defun emacsvox-ecb-speak-window-sources ()
  "Speak contents of sources window."
  (interactive)
  (save-excursion
    (save-window-excursion
      (ecb-goto-window-sources)
      (emacsvox-speak-buffer))))

(provide 'emacsvox-ecb)
;;;  end of file

